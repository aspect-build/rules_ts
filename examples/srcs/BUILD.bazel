"""Shows different ways to pass source files into ts_project"""

load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@bazel_lib//lib:params_file.bzl", "params_file")
load("@bazel_lib//lib:testing.bzl", "assert_contains")
load("@bazel_skylib//rules:write_file.bzl", "write_file")

# By default, the macro collects all .ts files in this directory.
# See the docs on srcs for exactly which files are included.
ts_project(
    name = "srcs-auto",
    out_dir = "auto",
)

# The sources can come from the default output of some other target.
# In this case, the ts_project macro won't be able to enumerate individual files
# so it cannot pre-declare the corresponding .js outputs.
filegroup(
    name = "srcs",
    srcs = glob(["*.ts"]),
)

ts_project(
    name = "srcs-filegroup",
    srcs = [":srcs"],
    out_dir = "filegroup",
)

# Tools can also generate .ts source files, so the sources are actually in the bazel-out tree.
# This example just writes one directly but this could instead be some tool.
write_file(
    name = "code_generation",
    out = "generated.ts",
    content = [
        "export const data: string[] = []",
    ],
)

# Writes output to bazel-bin/examples/simple/build/foo.js
ts_project(
    name = "srcs-generated",
    # Demonstrates that you can mix sources with generated files
    srcs = [
        "a.ts",
        "generated.ts",
    ],
    out_dir = "generated",
)

# Testing what outputs are actually produced
[
    params_file(
        name = "srcs-{}_mf".format(case),
        out = "srcs-{}.mf".format(case),
        args = ["$(rootpaths :srcs-{})".format(case)],
        data = [":srcs-" + case],
    )
    for case in [
        "auto",
        "filegroup",
        "generated",
    ]
]

[
    assert_contains(
        name = "test_" + case,
        actual = "srcs-{}.mf".format(case),
        expected = "srcs/{}/a.js".format(case),
    )
    for case in [
        "auto",
        "filegroup",
        "generated",
    ]
]

assert_contains(
    name = "test_gen",
    actual = "generated/generated.js",
    expected = "exports.data = []",
)
