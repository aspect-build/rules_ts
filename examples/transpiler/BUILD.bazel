load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@bazel_lib//lib:testing.bzl", "assert_outputs")
load("@bazel_skylib//lib:partial.bzl", "partial")
load("@bazel_skylib//rules:build_test.bzl", "build_test")

# Note, Bazel 6 starlark has lambda so maybe we can stop using partial
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load(":babel.bzl", "babel")
load(":tsc.bzl", "tsc_dts", "tsc_js")

###
# See a more full-featured example showing swc and babel at
# https://github.com/aspect-build/bazel-examples/tree/main/ts_project_transpiler
###

# Our test fixture is a large enough file that the transpilation difference is measurable
write_file(
    name = "gen_ts",
    out = "big.ts",
    content = [
        "export const a{0}: number = {0}".format(x)
        for x in range(10000)
    ],
)

write_file(
    name = "gen_a",
    out = "a.ts",
    content = [
        "export const b: number = 43",
    ],
)

write_file(
    name = "gen_c_js",
    out = "c.js",
    content = [
        "export const c = 44",
    ],
)

write_file(
    name = "gen_c_dts",
    out = "c.d.ts",
    content = [
        "export const c: number",
    ],
)

write_file(
    name = "gen_typecheck_fail",
    out = "typecheck_fail.ts",
    content = [
        "export const c: string = 43",
    ],
)

# Runs babel to transpile ts -> js
# and tsc to type-check
ts_project(
    name = "babel",
    srcs = ["big.ts"],
    declaration = True,
    out_dir = "build-babel",
    source_map = True,
    transpiler = babel,
)

# Runs babel to transpile ts -> js
# and does not produce any declaration outputs due to noEmit=True.
# Type-checking will be a separate action and target.
ts_project(
    name = "no-emit",
    srcs = ["big.ts"],
    no_emit = True,
    transpiler = babel,
    tsconfig = {
        "compilerOptions": {
            "noEmit": True,
        },
    },
)

# Runs babel to transpile ts -> js
# and does not produce any declaration outputs due to declaration=False.
# Type-checking will be a separate action and target.
ts_project(
    name = "no-declarations",
    srcs = ["a.ts"],
    out_dir = "build-nodecls",
    transpiler = babel,
    tsconfig = {
        "compilerOptions": {
            "declaration": False,
        },
    },
)

build_test(
    name = "targets_test",
    targets = [
        # babel ts_project
        ":babel",
        ":babel_typecheck",
        ":babel_typecheck_test",
        # babel outputted js
        "big.js",  # NOTE: does not implement out_dir in this test
        # tsc outputted dts
        "build-babel/big.d.ts",

        # no-emit for type-checking
        ":no-emit",
        ":no-emit_typecheck",
        ":no-emit_typecheck_test",

        # babel outputted .js with no declarations
        ":no-declarations",
        ":no-declarations_typecheck",
        ":no-declarations_typecheck_test",
        "a.js",  # NOTE: does not implement out_dir in this test
    ],
)

# Runs babel to transpile ts -> js while tsc would fail with typecheck errors
# Tagging as manual should prevent the typecheck from running even when requesting
# transpiler output files.
ts_project(
    name = "typecheck_fail",
    srcs = ["typecheck_fail.ts"],
    declaration = True,
    source_map = True,
    tags = ["manual"],
    transpiler = babel,
)

build_test(
    name = "targets_manual_test",
    targets = [
        ":typecheck_fail",
        "typecheck_fail.js",
        "typecheck_fail.js.map",
    ],
)

# custom js & dts transpilers, tsc still run for typechecking
ts_project(
    name = "custom_transpilers",
    srcs = ["a.ts"],
    declaration = True,
    declaration_transpiler = partial.make(
        tsc_dts,
        out_dir = "build-custom_transpilers",
    ),
    out_dir = "build-custom_transpilers",
    source_map = True,
    transpiler = partial.make(
        tsc_js,
        out_dir = "build-custom_transpilers",
    ),
)

build_test(
    name = "custom_transpilers_test",
    targets = [
        ":custom_transpilers",
        ":custom_transpilers_types",
        ":custom_transpilers_typecheck",
        ":custom_transpilers_typecheck_test",
        "build-custom_transpilers/a.js",
        "build-custom_transpilers/a.d.ts",
    ],
)

# custom js & dts transpilers, noEmit prevents any outputs, typechecking still exists
ts_project(
    name = "custom_transpilers-no_emit",
    srcs = ["a.ts"],
    declaration_transpiler = tsc_dts,
    transpiler = tsc_js,
    tsconfig = {
        "compilerOptions": {
            "noEmit": True,
            "declaration": True,
            "emitDeclarationOnly": True,
        },
    },
)

build_test(
    name = "custom_transpilers-no_emit-test",
    targets = [
        ":custom_transpilers-no_emit",
        ":custom_transpilers-no_emit_typecheck",
    ],
)

# custom dts transpiler, noEmit prevents any outputs, typechecking still exists
ts_project(
    name = "custom_dts_transpiler-no_emit",
    srcs = ["a.ts"],
    declaration_transpiler = tsc_dts,
    tsconfig = {
        "compilerOptions": {
            "noEmit": True,
            "declaration": True,
            "emitDeclarationOnly": True,
        },
    },
)

build_test(
    name = "custom_dts_transpiler-no_emit-test",
    targets = [
        ":custom_dts_transpiler-no_emit",
        ":custom_dts_transpiler-no_emit_typecheck",
    ],
)

# custom dts transpiler but still invokes tsc for type-checking
ts_project(
    name = "custom_dts_transpiler",
    srcs = ["a.ts"],
    declaration = True,
    declaration_transpiler = partial.make(
        tsc_dts,
        out_dir = "build-custom_dts_transpiler",
    ),
    out_dir = "build-custom_dts_transpiler",
    source_map = True,
    transpiler = "tsc",
)

build_test(
    name = "custom_dts_transpiler-test",
    targets = [
        ":custom_dts_transpiler",
        ":custom_dts_transpiler_types",
        ":custom_dts_transpiler_typecheck",
        ":custom_dts_transpiler_typecheck_test",
        "build-custom_dts_transpiler/a.js",
        "build-custom_dts_transpiler/a.d.ts",
    ],
)

# a custom dts transpiler but not actually outputting types
# should stylle have a _typecheck target
ts_project(
    name = "custom_dts_transpiler-no_declarations",
    srcs = ["a.ts"],
    declaration_transpiler = partial.make(
        tsc_dts,
        out_dir = "build-custom_dts_transpiler-no_declarations",
    ),
    out_dir = "build-custom_dts_transpiler-no_declarations",
    source_map = True,
    transpiler = "tsc",
    tsconfig = {
        "compilerOptions": {
            "declaration": False,
        },
    },
)

build_test(
    name = "custom_dts_transpiler-no_declarations-test",
    targets = [
        ":custom_dts_transpiler-no_declarations",
        ":custom_dts_transpiler-no_declarations_typecheck",
        ":custom_dts_transpiler-no_declarations_typecheck_test",
        "build-custom_dts_transpiler-no_declarations/a.js",
    ],
)

# custom dts transpiler and only outputting declarations
ts_project(
    name = "custom_dts_transpiler-declarations_only",
    srcs = ["a.ts"],
    declaration_transpiler = partial.make(
        tsc_dts,
        out_dir = "build-custom_dts_transpiler-no_declarations",
    ),
    out_dir = "build-custom_dts_transpiler-no_declarations",
    transpiler = "tsc",
    tsconfig = {
        "compilerOptions": {
            "declaration": True,
            "emitDeclarationOnly": True,
        },
    },
)

build_test(
    name = "custom_dts_transpiler-declarations_only-test",
    targets = [
        ":custom_dts_transpiler-declarations_only",
        ":custom_dts_transpiler-declarations_only_typecheck",
        ":custom_dts_transpiler-declarations_only_typecheck_test",
        "build-custom_dts_transpiler-no_declarations/a.d.ts",
    ],
)

ts_project(
    name = "declarations_tsc",
    srcs = [
        "a.ts",
        "c.d.ts",
    ],
    out_dir = "build-declarations_tsc",
    transpiler = "tsc",
    tsconfig = {
        "compilerOptions": {
            "sourceMap": True,
        },
    },
)

assert_outputs(
    name = "declarations_tsc_test",
    actual = ":declarations_tsc",
    expected = [
        "transpiler/build-declarations_tsc/a.js",
        "transpiler/build-declarations_tsc/a.js.map",
    ],
)

ts_project(
    name = "declarations_custom",
    srcs = [
        "a.ts",
        "c.d.ts",
    ],
    out_dir = "build-declarations_custom",
    transpiler = partial.make(
        babel,
        out_dir = "build-declarations_custom",
    ),
    tsconfig = {
        "compilerOptions": {
            "sourceMap": True,
        },
    },
)

assert_outputs(
    name = "declarations_custom_test",
    actual = ":declarations_custom",
    expected = [
        "transpiler/build-declarations_custom/a.js",
        "transpiler/build-declarations_custom/a.js.map",
    ],
)

# input .js files with tsc transpiler and an outDir
ts_project(
    name = "allow_js_outdir",
    srcs = ["d.js"],
    out_dir = "build-js_outdir",
    transpiler = "tsc",
    tsconfig = {
        "compilerOptions": {
            "allowJs": True,
            "module": "commonjs",
            "sourceMap": True,
        },
    },
)

assert_outputs(
    name = "allow_js_outdir_test",
    actual = ":allow_js_outdir",
    expected = [
        "examples/transpiler/build-js_outdir/d.js",
        "examples/transpiler/build-js_outdir/d.js.map",
    ],
)

# input .js files with custom transpiler and an outDir
ts_project(
    name = "allow_js_babel_outdir",
    srcs = ["d.js"],
    out_dir = "build-js_babel_outdir",
    transpiler = partial.make(
        babel,
        out_dir = "build-js_babel_outdir",
    ),
    tsconfig = {
        "compilerOptions": {
            "allowJs": True,
            "module": "commonjs",
            "sourceMap": True,
        },
    },
)

assert_outputs(
    name = "allow_js_babel_outdir_test",
    actual = ":allow_js_babel_outdir",
    expected = [
        "examples/transpiler/build-js_babel_outdir/d.js",
        "examples/transpiler/build-js_babel_outdir/d.js.map",
    ],
)

# input .js files with custom transpiler and NO outDir
ts_project(
    name = "allow_js_babel_no_outdir",
    srcs = ["d.js"],
    transpiler = babel,
    tsconfig = {
        "compilerOptions": {
            "allowJs": True,
            "module": "commonjs",
            "sourceMap": True,
        },
    },
)

assert_outputs(
    name = "allow_js_babel_no_outdir_test",
    actual = ":allow_js_babel_no_outdir",
    expected = [
        "examples/transpiler/d.js",
        "examples/transpiler/d.js.map",
    ],
)

# input .js files with tsc transpiler and NO outDir
# TODO: broken due to all source .js being copied to bin before invoking tsc
# ts_project(
#     name = "allow_js",
#     srcs = ["d.js"],
#     transpiler = "tsc",
#     tsconfig = {
#         "compilerOptions": {
#             "allowJs": True,
#             "module": "commonjs",
#             "sourceMap": True,
#         },
#     }
# )

# assert_outputs(
#     name = "allow_js_test",
#     actual = ":allow_js",
#     expected = [
#         "examples/transpiler/d.js",
#         "examples/transpiler/d.js.map",
#     ],
# )
