"""Shows how https://www.typescriptlang.org/tsconfig/#resolveJsonModule can be used."""

load("@aspect_bazel_lib//lib:testing.bzl", "assert_contains")
load("@aspect_rules_js//js:defs.bzl", "js_test")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")

ts_project(
    name = "ts",
    srcs = [
        "data.json",
        "index.ts",
    ],
    resolve_json_module = True,
)

ts_project(
    name = "ts-dict-override",
    srcs = [
        "data.json",
        "index.ts",
    ],
    extends = "tsconfig.json",
    tsconfig = {
        "compilerOptions": {
            "outDir": "ts-dict-override",
            "resolveJsonModule": True,
        },
        "exclude": [],
    },
)

ts_project(
    name = "ts-dict-unspecified",
    srcs = [
        "data.json",
        "index.ts",
    ],
    extends = "tsconfig.json",
    resolve_json_module = True,
    tsconfig = {
        "compilerOptions": {
            "outDir": "ts-dict-unspecified",
        },
        "exclude": [],
    },
)

assert_contains(
    name = "test",
    actual = "index.js",
    expected = """JSON.stringify(data_json_1.default)""",
)

js_test(
    # Test that the json is available at runtime.
    name = "ts-with-json",
    data = [":ts"],
    entry_point = "index.js",
)

# Test that the json is available at runtime with various ways of
# specifying resolveJsonModule in tsconfig.json.
[
    js_test(
        name = "ts-with-json-%s" % t,
        data = [":ts-%s" % t],
        entry_point = "ts-%s/index.js" % t,
    )
    for t in [
        "dict-override",
        "dict-unspecified",
    ]
]
