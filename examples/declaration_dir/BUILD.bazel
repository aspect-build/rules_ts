"""Shows how https://www.typescriptlang.org/tsconfig/#declarationDir can be used to write
.d.ts typings files to a different folder than the JavaScript outputs."""

load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@bazel_lib//lib:testing.bzl", "assert_outputs")

ts_project(
    name = "transpile",
    srcs = ["dir/lib.ts"],
    declaration = True,
    declaration_dir = "out/types",
    declaration_map = True,
    out_dir = "out/code",
    root_dir = "dir",
    source_map = True,
)

# Assert that the output locations we wrote to match expectations
assert_outputs(
    name = "check_transpile",
    actual = ":transpile",
    expected = [
        "declaration_dir/out/code/lib.js",
        "declaration_dir/out/code/lib.js.map",
    ],
)

# Convert the types output group to the default output
filegroup(
    name = "types",
    srcs = [":transpile"],
    output_group = "types",
)

assert_outputs(
    name = "check_types",
    actual = ":types",
    expected = [
        "declaration_dir/out/types/lib.d.ts",
        "declaration_dir/out/types/lib.d.ts.map",
    ],
)
