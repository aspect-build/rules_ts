"""Shows how https://www.typescriptlang.org/tsconfig/#declarationDir can be used to write
.d.ts typings files to a different folder than the JavaScript outputs."""

load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@aspect_bazel_lib//lib:params_file.bzl", "params_file")

ts_project(
    name = "transpile",
    srcs = ["dir/lib.ts"],
    declaration = True,
    declaration_dir = "out/types",
    declaration_map = True,
    out_dir = "out/code",
    root_dir = "dir",
    source_map = True,
)

# Convert the types output group to the default output
filegroup(
    name = "types",
    srcs = [":transpile"],
    output_group = "types",
)

# Write a manifest file showing all the output locations we wrote to
params_file(
    name = "params",
    out = "outputs.txt",
    args = [
        "$(rootpaths :types)",
        "$(rootpaths :transpile)",
    ],
    data = [
        ":transpile",
        ":types",
    ],
)

# Assert that it matches our expectations
write_source_files(
    name = "test",
    files = {
        "expected_outputs.txt": "outputs.txt",
    },
)
