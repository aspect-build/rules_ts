load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@bazel_skylib//rules:write_file.bzl", "write_file")

# Dependencies for all targets in this package
_DEPS = [
    "//examples:node_modules/@myorg/js_lib",
    "//examples:node_modules/@myorg/dts_lib",
    "//examples:node_modules/@types/node",
    "//examples:node_modules/date-fns",
]

# Type-checks, and emits output to
# bazel-bin/examples/simple/foo.js
# bazel-bin/examples/simple/foo.d.ts
ts_project(
    name = "ts",
    srcs = ["foo.ts"],
    declaration = True,
    # Note, the tsconfig attribute defaults to the tsconfig.json file in this directory.
    # tsconfig = "<default>",
    deps = _DEPS,
)

# Code generation tools can produce .ts outputs.
# This example just writes one directly to bazel-bin.
write_file(
    name = "code_generation",
    out = "generated.ts",
    content = [
        "export const data: string[] = []",
    ],
)

# Writes output to bazel-bin/examples/simple/build/foo.js
ts_project(
    name = "outdir",
    # Demonstrates that you can mix sources with generated files
    srcs = [
        "foo.ts",
        "generated.ts",
    ],
    # A tsconfig.json file will be generated with this content
    tsconfig = {
        "compilerOptions": {
            "outDir": "build",
        },
    },
    deps = _DEPS,
)
