"""# Protocol Buffers and gRPC (UNSTABLE)

**UNSTABLE API**: contents of this page are not subject to our usual semver guarantees.
We may make breaking changes in any release.
Please try this API and provide feedback.
We intend to promote it to a stable API in a minor release, possibly as soon as v2.1.0.

`ts_proto_library` uses the Connect library from bufbuild, and supports both Web and Node.js:

- https://connectrpc.com/docs/web/getting-started
- https://connectrpc.com/docs/node/getting-started

This Bazel integration follows the "Local Generation" mechanism described at
https://connectrpc.com/docs/web/generating-code#local-generation,
using packages such as `@bufbuild/protoc-gen-es`, `@connectrpc/protoc-gen-connect-query`, and `@connectrpc/protoc-gen-connect-es`
as plugins to protoc.

The [aspect configure](https://docs.aspect.build/cli/commands/aspect_configure) command
auto-generates `ts_proto_library` rules as of the 5.7.2 release.
It's also possible to compile this library into your Gazelle binary.

Note: this API surface is not included in `defs.bzl` to avoid eager loads of rules_proto for all rules_ts users.

Installation
------------

If you install rules_ts in `WORKSPACE`, you'll need to install the deps of rules_proto, like this:

```
load("@rules_proto//proto:repositories.bzl", "rules_proto_dependencies")

rules_proto_dependencies()
```

If you use bzlmod/`MODULE.bazel` then no extra install is required.

Future work
-----------

- Allow users to choose other plugins. We intend to wait until http://github.com/bazelbuild/rules_proto supports protoc plugins.
- Allow users to control the output format. Currently it is hard-coded to `js+dts`, and the JS output uses ES Modules.
"""

load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@aspect_bazel_lib//lib:directory_path.bzl", "directory_path", "make_directory_path")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@aspect_rules_js//js:defs.bzl", "js_binary", "js_library")
load("//ts/private:ts_proto_library.bzl", ts_proto_library_rule = "ts_proto_library")

def ts_proto_library(name, node_modules, proto, protoc_gen_options = {}, gen_connect_es = True, gen_connect_query = False, gen_connect_query_service_mapping = {}, copy_files = True, proto_srcs = None, files_to_copy = None, **kwargs):
    """
    A macro to generate JavaScript code and TypeScript typings from .proto files.

    Args:
        name: name of resulting ts_proto_library target
        node_modules: Label pointing to the linked node_modules target where @bufbuild/protoc-gen-es is linked, e.g. //:node_modules.
            Since the generated code depends on @bufbuild/protobuf, this package must also be linked.
            If `gen_connect_es = True` then @bufbuild/proto-gen-connect-es should be linked as well.
            If `gen_connect_query = True` then @bufbuild/proto-gen-connect-query should be linked as well.
        proto: the `proto_library` target that contains the .proto files to generate code for.
        protoc_gen_options: dict of protoc_gen_es options.
          See https://github.com/bufbuild/protobuf-es/tree/main/packages/protoc-gen-es#plugin-options
        gen_connect_es: whether protoc_gen_connect_es should generate grpc services, and therefore `*_connect.{js,d.ts}` should be written.
        gen_connect_query: whether protoc_gen_connect_query should generate [TanStack Query](https://tanstack.com/query) clients, and therefore `*_connectquery.{js,d.ts}` should be written.
        gen_connect_query_service_mapping: mapping from source proto file to the named RPC services that file contains.
          Needed to predict which files will be generated by gen_connect_query.
          See https://github.com/connectrpc/connect-query-es/tree/main/examples/react/basic/src/gen

          For example, given `a.proto` which contains a service `Foo` and `b.proto` that contains a service `Bar`,
          the mapping would be `{"a.proto": ["Foo"], "b.proto": ["Bar"]}`
        copy_files: whether to copy the resulting `.d.ts` files back to the source tree, for the editor to locate them.
        proto_srcs: the .proto files that are being generated. Repeats the `srcs` of the `proto_library` target.
            This is used only to determine a default for `files_to_copy`.
        files_to_copy: which files from the protoc output to copy. By default, performs a replacement on `proto_srcs` with the typical output filenames.
        **kwargs: additional named arguments to the ts_proto_library rule
    """
    if type(node_modules) != "string":
        fail("node_modules should be a label, not a " + type(node_modules))
    protoc_gen_es_target = "_{}.gen_es".format(name)
    protoc_gen_es_entry = protoc_gen_es_target + "__entry_point"

    # Reach into the node_modules to find the entry points
    directory_path(
        name = protoc_gen_es_entry,
        tags = ["manual"],
        directory = node_modules + "/@bufbuild/protoc-gen-es/dir",
        path = "bin/protoc-gen-es",
    )
    js_binary(
        name = protoc_gen_es_target,
        data = [
            node_modules + "/@bufbuild/protoc-gen-es",
            node_modules + "/@bufbuild/protobuf",
        ],
        entry_point = protoc_gen_es_entry,
    )

    protoc_gen_connect_es_target = None
    if gen_connect_es:
        protoc_gen_connect_es_target = "_{}.gen_connect_es".format(name)
        protoc_gen_connect_es_entry = protoc_gen_connect_es_target + "__entry_point"
        directory_path(
            name = protoc_gen_connect_es_entry,
            tags = ["manual"],
            directory = node_modules + "/@connectrpc/protoc-gen-connect-es/dir",
            path = "bin/protoc-gen-connect-es",
        )
        js_binary(
            name = protoc_gen_connect_es_target,
            data = [node_modules + "/@connectrpc/protoc-gen-connect-es"],
            entry_point = protoc_gen_connect_es_entry,
        )

    protoc_gen_connect_query_target = None
    if gen_connect_query:
        protoc_gen_connect_query_target = "_{}.gen_connect_query".format(name)
        protoc_gen_connect_query_entry = protoc_gen_connect_query_target + "__entry_point"
        directory_path(
            name = protoc_gen_connect_query_entry,
            tags = ["manual"],
            directory = node_modules + "/@connectrpc/protoc-gen-connect-query/dir",
            path = "bin/protoc-gen-connect-query",
        )
        js_binary(
            name = protoc_gen_connect_query_target,
            data = [node_modules + "/@connectrpc/protoc-gen-connect-query"],
            entry_point = protoc_gen_connect_query_entry,
        )

    protoc_target = "_{}_protoc".format(name)
    ts_proto_library_rule(
        name = protoc_target,
        protoc_gen_es = protoc_gen_es_target,
        protoc_gen_connect_es = protoc_gen_connect_es_target,
        protoc_gen_connect_query = protoc_gen_connect_query_target,
        gen_connect_es = gen_connect_es,
        gen_connect_query = gen_connect_query,
        gen_connect_query_service_mapping = gen_connect_query_service_mapping,
        proto = proto,
        protoc_gen_options = protoc_gen_options,
    )

    js_library(
        name = name,
        srcs = [":{}".format(protoc_target)],
        # The codegen always has a runtime dependency on the protobuf runtime
        # TODO: could we collect them with an aspect?
        deps = kwargs.pop("deps", []) + [node_modules + "/@bufbuild/protobuf"],
        **kwargs
    )

    if not copy_files:
        return
    if not files_to_copy:
        if not proto_srcs:
            fail("Either proto_srcs should be set, or copy_files should be False")
        files_to_copy = [s.replace(".proto", "_pb.d.ts") for s in proto_srcs]
        if gen_connect_es:
            files_to_copy.extend([s.replace(".proto", "_connect.d.ts") for s in proto_srcs])
        if gen_connect_query:
            for proto, services in gen_connect_query_service_mapping.items():
                files_to_copy.extend([proto.replace(".proto", "-{}_connectquery.d.ts".format(s)) for s in services])

    files_target = "_{}.filegroup".format(name)
    dir_target = "_{}.directory".format(name)
    copy_target = "{}.copy".format(name)

    native.filegroup(
        name = files_target,
        srcs = [name],
        output_group = "types",
    )

    copy_to_directory(
        name = dir_target,
        srcs = [files_target],
        root_paths = ["**"],
    )

    write_source_files(
        name = copy_target,
        files = {
            f: make_directory_path("_{}_dirpath".format(f), dir_target, f)
            for f in files_to_copy
        },
    )
